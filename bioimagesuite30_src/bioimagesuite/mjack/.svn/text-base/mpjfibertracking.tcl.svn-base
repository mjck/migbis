
#!/bin/sh
# the next line restarts using wish \
    exec vtk "$0" "$@"

# -----------------------
# Dependencies and path
# -----------------------
lappend auto_path [ file dirname [ info script ] ]
lappend auto_path [ file join [file join [ file dirname [ info script ]] ".."] base ]
lappend auto_path [ file join [file join [ file dirname [ info script ]] ".."] main ]

package provide mpjfibertracking 1.2

package require  Itcl 3.2
package require  Iwidgets 4.0

package require  pxitcllistselector 1.0
package require  pxitclbaseimagecontrol 1.0
package require  pxitclbaseimageviewer 1.0

package require  vtkmpjcontrib 1.1
package require  mpjguilookuptable 1.0

itcl::class mpjfibertracking {

    inherit pxitclbaseimagecontrol
    
    #-----------------------------------
    # construction and destruction
    #-----------------------------------
    constructor { par } {
	pxitclbaseimagecontrol::constructor $par
    } { InitializeControl }
    
    destructor { }
    
    #-----------------------------------
    # common variables
    #-----------------------------------

    #-----------------------------------
    # global variables
    #-----------------------------------
    private variable widget_list
    private variable global_list
    
    #-----------------------------------
    # initialization methods
    #-----------------------------------
    public method Initialize { inpwidg }
    public method InitializeControl { }
    public method DismissWindow { }
    
    #-----------------------------------
    # input/output
    #-----------------------------------
    public method LoadMap { fname }
    public method LoadMask { fname }
    public method LoadTensor { fname }
    public method SaveAllResults { }
    public method SaveResult { index }
    public method ClearAllResults { }
    public method DisplayResult { index }
    public method ClearDisplay { }
    public method SetResultPreffix { preffix }
    public method GetResultPreffix { } 
    public method SetCurrentDirectory { fname } 
    
    #-----------------------------------
    # interface creation methods
    #-----------------------------------
    private method CreateInputControl { base }
    private method CreateDirectionControl { base }
    private method CreateTrackingControl { base }
    private method CreateDisplayControl { base }
    private method CreateFiberControl { base }
    private method CreateStatisticsControl { base }    
    private method CreateResultsControl { base }
    private method CreateFiberDisplayControl { base }
    private method CreateMenu { mb }
    public method AddToMenuButton { mb args}    
    public method CreateViewMenu { menubase }
 
    private method loadmap { }
    private method loadtensor { }
    private method loadmask { }
    private method displayresult { }
    private method saveresult { }
    private method toggleresult { }
    private method saveallresults { }
 
    private method initialize_glyphs { }
    private method input_updateinfo { index }    
    private method input_selectioncommand_cb { }   
    
    private method track_seedtype_cb { }
    private method track_integration_cb { }
    private method track_eigenvector_cb { }
    private method track_regionfilter_cb { }

    private method view_colorbar_cb { } 
    private method view_directionscheme_cb { }
    private method view_update_display_cb { }

    public method  view_update_crosshair_cb { arg }
    private method view_toggle_trackexplore { }

    private method view_clear_display { }
    private method view_update_display { }
    private method view_toggle_colorbar { }
    private method view_toggle_fiber { }

    private method view_create_fiber_glyph { index }
    private method view_create_fibers { }
    private method view_create_colorbar { }

    private method view_fiber_glyph_cb { }
    private method view_fiber_tuberadius_cb { }
    private method view_fiber_pointsize_cb { }
    private method view_fiber_colormap_cb { }
    private method view_fiber_cmapresetbtn_cb { }
    private method view_fiber_cmaploadbtn_cb { }
    private method view_fiber_cmapsavebtn_cb { }
    private method view_fiber_filter_cb { }
          
    private method fiber_add { polydata }
    private method fiber_addwithname { name polydata }
    private method fiber_remove { index } 
    private method fiber_removeselected { }
    private method fiber_clear { }
    private method fiber_load { }
    private method fiber_save { index name }
    private method fiber_saveall { }
    private method fiber_saveselected { }
    private method fiber_track { }

    private method fiber_explore { cx cy cz }

    private method fiber_statistics { }
    private method fiber_quantize { }
    private method fiber_addstatistics { title results tag }
    private method fiber_beginstatistics { title overwrite }
    private method fiber_savestatistics { }
    private method fiber_selection_cb { }
    private method fiber_color_cb { }
    private method fiber_name_cb { }
    private method fiber_display_cb { }

    private method result_clear { }
    private method update_list { node }    

    private method savedir_dialog { base initdir title }
    private method openfile_dialog { base initdir title mask }
}  

#-------------------------------------------------------------------------------------------
#  Close window 
# -------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::DismissWindow { } {
    
    set ok "yes"

    if { $global_list(tensor_fname) != "" } {

	set ok [ tk_messageBox -type yesnocancel -default no \
		     -parent $basewidget -title "Think again ..." \
		     -message "Unload all images from memory?" -icon question  ]
    }
    
    if { $ok == "yes" } {	

	
	fiber_clear
	
	view_clear_display 
	
	$this ClearAllResults
	
	$this SetTitle $global_list(appname)
	
	set global_list(mask_fname) ""
	set global_list(tensor_fname) ""
	
    }
    
    if { $ok != "cancel" } {
	$this HideWindow
    }
}

#-------------------------------------------------------------------------------------------
#  Initialize tool
# -------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::InitializeControl { } {

    set global_list(appname) "Fiber Tracking Utility"
    
    set appname "$global_list(appname) $version"
    set aboutstring "(c) Marcel Jackowski 2008"
    
    # create list of basic colors
    set global_list(tkcolor_list) "\#ff0000 \#ffff00 \#00ff00 \#00ffff \#0000ff \#ff00ff"
    
    # create image titles
    set global_list(result_title) \
	[ list \
	      { "Diffusion tensor"     "dti_tensor" 0 } \
	      { "Regions of interest"  "dti_roi" 0 } \
	      { "Map for analysis"     "dti_map" 0 } \
	      { "Fiber quantization"   "dti_fiber" 1 } ]
    
    foreach item $global_list(result_title) {
 	lappend result_list [ pxitclimage \#auto ]
    }  
    
    set global_list(result_fdir) [ pwd ]
    set global_list(result_fpreffix) "preffix_"
    set global_list(result_list) $result_list
    
    set global_list(mask_fname) ""
    set global_list(mask_nreg) 1
    
    set global_list(tensor_fname) ""    
    set global_list(map_fname) ""
    
    set global_list(track_lastpoint) { 0 0 0 }
    set global_list(track_seedtype) 0
    set global_list(track_seedregion) 1
    set global_list(track_seedwidth) 3
    set global_list(track_seedheight) 3
    set global_list(track_seeddepth) 3
    set global_list(track_seeddens) 1.0  
    set global_list(track_integration) 1
    set global_list(track_eigenvector) 0
    set global_list(track_steplen) 0.25
    set global_list(track_maxdist) 100.0
    set global_list(track_mindist) 10.0
    set global_list(track_maxangle) 45.0
    set global_list(track_minFA) 0.2
    set global_list(track_maxFA) 1.0
    set global_list(track_minMD) 0.0
    set global_list(track_maxMD) 10.0   
    set global_list(track_regionfilter) 0
    set global_list(track_regionnumber) 1
    set global_list(track_clipping) 0
    set global_list(track_autostat) 1
    set global_list(track_flipping) 1
    set global_list(track_explore) 0
    set global_list(track_dec) 1
    set global_list(track_flipx) 0
    set global_list(track_flipy) 0
    set global_list(track_flipz) 0

    set global_list(fiber_display) 1
    set global_list(fiber_color) "\#ff0000"
    set global_list(fiber_list) {}
    set global_list(fiber_color_list) {}
    set global_list(fiber_disp_list) {}
    
    set global_list(stat_overwrite) 0

    set global_list(view_colorbar) 0 
    set global_list(view_colorbar_label) "Map"
    set global_list(view_fiber) 0
    set global_list(view_fiber_tuberadius) 0.20
    set global_list(view_fiber_pointsize) 1.0
    
    set global_list(view_direction_scheme) 0
    set global_list(view_direction_saturation) 0.5
    set global_list(view_direction_scaling) 1.0
    set global_list(view_direction_modulate) 0
    set global_list(view_direction_alpha) 0
    

    ## create actors for visualization
    set global_list(actor_colorbar) -1
    set global_list(actor_fiber) {}

    ## vtk data
    set global_list(vtk_fiber_tube) {}
    set global_list(vtk_fiber_polydata) {}
    
    initialize_glyphs
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Create visualization glyphs
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::initialize_glyphs { } {

    ## create line source
    set line_src [ vtkLineSource [ pxvtable::vnewobj ] ]
    $line_src SetPoint1 0.0 0 0
    $line_src SetPoint2 2.0 0 0    

    set line_xfrm [ vtkTransform [ pxvtable::vnewobj ] ]
    $line_xfrm Translate -1.0 0.0 0.0

    set line [ vtkTransformPolyDataFilter [ pxvtable::vnewobj ] ]
    $line SetInput [ $line_src GetOutput ]
    $line SetTransform $line_xfrm
    lappend global_list(glyph_list) $line 

    $line_src Delete
    $line_xfrm Delete
    
    ## create tube source
    set tube_src [ vtkCylinderSource [ pxvtable::vnewobj ] ]
    $tube_src SetHeight 2.0
    $tube_src SetRadius 0.25
    $tube_src CappingOn
    
    set tube_xfrm [ vtkTransform [ pxvtable::vnewobj ] ]
    $tube_xfrm RotateZ -90.0
    
    set tube [ vtkTransformPolyDataFilter [ pxvtable::vnewobj ] ]
    $tube SetInput [ $tube_src GetOutput ]
    $tube SetTransform $tube_xfrm
    lappend global_list(glyph_list) $tube

    $tube_src Delete
    $tube_xfrm Delete

    ## create arrow source
    set arrow_src [ vtkArrowSource [ pxvtable::vnewobj ] ]
    $arrow_src SetTipLength 0.35
    $arrow_src SetTipResolution 10
    $arrow_src SetTipRadius 0.20
    $arrow_src SetShaftResolution 10
    $arrow_src SetShaftRadius 0.1

    set arrow_xfrm [ vtkTransform [ pxvtable::vnewobj ] ]
    $arrow_xfrm Translate -1.0 0.0 0.0
    $arrow_xfrm Scale 2.0 2.0 2.0

    set arrow [ vtkTransformPolyDataFilter [ pxvtable::vnewobj ] ]
    $arrow SetInput [ $arrow_src GetOutput ]
    $arrow SetTransform $arrow_xfrm
    lappend global_list(glyph_list) $arrow

    $arrow_src Delete
    $arrow_xfrm Delete
    
    ## create ellipsoid source
    set ellipsoid_src [ vtkSphereSource [ pxvtable::vnewobj ] ]
    set ellipsoid_xfrm [ vtkTransform [ pxvtable::vnewobj ] ]
    $ellipsoid_xfrm Scale 2.0 2.0 2.0

    set ellipsoid [ vtkTransformPolyDataFilter [ pxvtable::vnewobj ] ]
    $ellipsoid SetInput [ $ellipsoid_src GetOutput ]
    $ellipsoid SetTransform $ellipsoid_xfrm
    lappend global_list(glyph_list) $ellipsoid

    $ellipsoid_src Delete
    $ellipsoid_xfrm Delete
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Load mask
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::loadmask { } {
    
    ## browse for filename
    set typelist { {"Analyze/NIFT1 Files" { .hdr .hdr.gz .nii .nii.gz }}}
    set fname  [tk_getOpenFile -title "Select mask" -filetypes $typelist -initialdir $global_list(result_fdir) -parent $basewidget ] 
    
    if { $fname != "" } {
	
	set global_list(result_fdir) [ file dirname $fname ]
	
	## load mask into memory
	LoadMask $fname
    }
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Load in mask image 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::LoadMask { fname } {

    set MASK 1     
    set mask [ lindex $global_list(result_list) $MASK ]
    
    ## load series into memory
    set ana [ vtkpxAnalyzeImageSource  [ pxvtable::vnewobj ]]
    $this SetFilterCallbacks $ana "Reading in mask image..."
    
    WatchOn
    
    ## load series 
    $ana Load $fname 
    
    ## if mask not a unsigned char image, convert it
    set scalar_type [ [ $ana GetOutput ] GetScalarType ]    

    if { $scalar_type != 3 } {	
	
	set imcast [ vtkImageCast [ pxvtable::vnewobj ] ]
	$this SetFilterCallbacks $imcast "Converting mask to unsigned char values..."
	$imcast SetInput [ $ana GetOutput ]
	$imcast SetOutputScalarTypeToUnsignedChar
	$imcast ClampOverflowOn	
	$imcast Update
	
	## fixed on 11/1/04
	$mask CopyImageHeader [ $ana GetImageHeader ]
	$mask ShallowCopyImage [ $imcast GetOutput ]
	
	$imcast Delete
	
    } else {
	
	$mask SetFromAnalyzeSource $ana
    }
   
    ## determine range of values in mask
    set range [ [ [ [ $mask GetImage ] GetPointData ] GetScalars ] GetRange ]

    ## take the maximum range as the number of regions in mask
    set global_list(mask_nreg) [ expr int([ lindex $range 1 ])]
    $widget_list(mask_nreg) configure -range [ list 1 $global_list(mask_nreg) ]
    
    set global_list(mask_fname) $fname

    WatchOff
    
    $ana Delete
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Load map
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::loadmap { } {

    ## browse for filename
    set typelist { {"Analyze/NIFT1 Files" { .hdr .hdr.gz .nii .nii.gz }}}
    set fname  [tk_getOpenFile -title "Select map" -filetypes $typelist -initialdir $global_list(result_fdir) -parent $basewidget ] 
    
    if { $fname != "" } {
	
	set global_list(result_fdir) [ file dirname $fname ]
	
	## load map into memory
	LoadMap $fname
    }    
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Load in map image 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::LoadMap { fname } {

    ## load series into memory
    set ana [ vtkpxAnalyzeImageSource  [ pxvtable::vnewobj ]]
    
    $this SetFilterCallbacks $ana "Reading in map image..."
    
    WatchOn
    
    ## load series 
    $ana Load $fname 
    
    [ lindex $global_list(result_list) 2 ] SetFromAnalyzeSource $ana
    
    set global_list(map_fname) $fname
    
    WatchOff
    
    $ana Delete
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Load tensor
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::loadtensor { } {

    ## browse for filename
    set typelist { {"Analyze/NIFT1 Files" { .hdr .hdr.gz .nii .nii.gz }}}
    set fname  [tk_getOpenFile -title "Select tensor" -filetypes $typelist -initialdir $global_list(result_fdir) -parent $basewidget ] 
        
    if { $fname != "" } {
	
	set global_list(result_fdir) [ file dirname $fname ]
	
	## load mask into memory
	LoadTensor $fname
    }
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Load in tensor image 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::LoadTensor { fname } {

    set TENSOR 0
    set MASK 1
    set MAP 2
    set FIBERS 3

    set tensor [ lindex $global_list(result_list) $TENSOR ]
    set fibers [ lindex $global_list(result_list) $FIBERS ]
    set map [lindex $global_list(result_list) $MAP ]

    WatchOn

    ## load series into memory
    set ana [ vtkpxAnalyzeImageSource  [ pxvtable::vnewobj ]]
    
    $this SetFilterCallbacks $ana "Reading in tensor image..."
    
    ## load series 
    $ana Load $fname 

    $tensor SetFromAnalyzeSource $ana

    set global_list(tensor_fname) $fname

    ## determine coordinate transform
    set or [ $ana GetOrientation ]
#    if { $or == 0 } {
# 	set global_list(track_flipx) 1
# 	set global_list(track_flipy) 1
#    } elseif { $or == 1 } {
# 	set global_list(track_flipx) 1
#    } 
    
    $ana Delete

    scan [ [ $tensor GetImage ] GetDimensions ] "%d %d %d" dx dy dz	       
    set nc [ [ $tensor GetImage ] GetNumberOfScalarComponents ]
    set name [ file tail [ file root $fname ] ]
        
    set sufpos [ string last "dti_tensor" $name ]
    if { $sufpos != -1 } {
	set name [ string range $name 0 [expr $sufpos - 1] ]
    }

    ## create fiber image
    set ext [ vtkImageExtractComponents [ pxvtable::vnewobj ] ]
    $ext SetInput [ $tensor GetImage ]
    $ext SetComponents 0
    $ext Update

    $fibers CopyImageHeader [ $tensor GetImageHeader ]
    $fibers ShallowCopyImage [ $ext GetOutput ]
    $ext Delete

    set global_list(result_fdir) [ file dir $fname ]
    set global_list(result_fpreffix) [ file join $global_list(result_fdir) "${name}" ]

    WatchOff

    $widget_list(trackbtn) buttonconfigure 0 -state normal
    $widget_list(menu_tracking) entryconfigure 0 -state normal
    $widget_list(menu_tracking) entryconfigure 2 -state normal
    
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Set result filename preffix 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::SetResultPreffix { preffix } {
    set global_list(result_fpreffix) $preffix
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Get result filename preffix 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::GetResultPreffix { } {
    return $global_list(result_fpreffix)
}

itcl::body mpjfibertracking::SetCurrentDirectory { fname } {
    set global_list(result_fdir) $fname
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Clear all results
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::ClearAllResults { } {
    
    foreach result $global_list(result_list) {
	$result Clear
    }
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Save all results
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::SaveAllResults { } {

    foreach result $global_list(result_list) title $global_list(result_title) {
	
	set fname $global_list(result_fpreffix)
	set fname "$fname[ lindex $title 1 ]"
	
	$result Save $fname
    }
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Save single result
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::SaveResult { index } {        

    set fname $global_list(result_fpreffix)
    set fname "$fname[ lindex [lindex $global_list(result_title) $index ] 1 ]"

    [lindex $global_list(result_list) $index] Save $fname
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Save all results
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::saveallresults { } {

    ## browse for directory
    set fdir [ tk_chooseDirectory -title " Select directory" -parent $basewidget -initialdir $global_list(result_fdir) ]
    
    if { $fdir != "" } {
	
	set global_list(result_fdir) $fdir
	
	foreach result $global_list(result_list) title $global_list(result_title) {
	    
	    set fname $global_list(result_fpreffix)
	    set fname "$fname[ lindex $title 1 ]"
	    
	    $result Save [ file join $fdir $fname ]
	}
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Save results
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::saveresult { } {
    
    ## get current selection 
    set sel_item [ $widget_list(result_list) getcurselection ]
    
    if { $sel_item != "" } { 

	## browse for directory
	set fdir [ tk_chooseDirectory -title " Select directory" -parent $basewidget -initialdir $global_list(result_fdir) ]
	
	if { $fdir != "" } {
	    
	    set global_list(result_fdir) $fdir
	    
	    foreach item $sel_item {
		
		set index [ $widget_list(result_list) index $item ]
		set result [ lindex $global_list(result_list) $index ]
		
		set fname $global_list(result_fpreffix)
		set fname "$fname[ lindex [lindex $global_list(result_title) $index ] 1 ]"
		
		$result Save [ file join $fdir $fname ]
	    }
	}
    }
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Display result
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::DisplayResult { index } {
    $parent SetImageFromObject \
	[ [ lindex $global_list(result_list) $index ] GetThisPointer ] $this
    $parent ShowWindow
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Display result
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::displayresult { } {

    set sel_item [ $widget_list(result_list) getcurselection ]
	    
    if { $sel_item != "" } {
	set index [ $widget_list(result_list) index [ lindex $sel_item 0 ] ]
	set result [ lindex $global_list(result_list) $index ]
	
	$this DisplayResult $index	
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: toggle result computation on/off
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::toggleresult { } {
   
    set color(0) \#dcdcdc
    set color(1) black

    set sel_item [ $widget_list(result_list) getcurselection ]

    if { $sel_item != "" } {

	set index [ $widget_list(result_list) index [ lindex $sel_item 0 ] ]

	if { $index > 2 } {
	    set enable [expr 1 - [ lindex [ lindex $global_list(result_title) $index ] 2 ] ]
	    
	    $widget_list(result_list) itemconfigure $index -foreground $color($enable)
	    
	    set item [ lindex $global_list(result_title) $index ]
	    set new_item [ lreplace $item 2 2 $enable ]
	    
	    set global_list(result_title) [ lreplace $global_list(result_title) \
						$index $index $new_item ]
	}
    }
    
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Clear results
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::result_clear { } {
    for { set i 0 } { $i < [ llength $global_list(result_list) ] } { incr i } {		
	if { $i > 0 } {	    
	    [ lindex $global_list(result_list) $i ] Clear	    
	}
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Clear display
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_clear_display { } {
    
    ## remove fibers
    foreach actor $global_list(actor_fiber) {	    
	if { $actor != "-1" } {
	    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] RemoveActor $actor	    
	    $actor Delete	    
	}
	set global_list(actor_fiber) {}
    }
    
    ## remove colorbar
    set actor $global_list(actor_colorbar)
    if { $actor != "-1" } {
	[ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] RemoveActor $actor	    
	$actor Delete	   
	set global_list(actor_colorbar) -1
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Update display
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_update_display { } {
    
    if { $global_list(view_colorbar) } {
	view_create_colorbar
    }
    
    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  Toggle On/Off colorbar visualization
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_toggle_colorbar { } {
    
    if { $global_list(view_colorbar) } {
	view_create_colorbar
    } else {
	set actor $global_list(actor_colorbar)
	[ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] RemoveActor $actor
	$actor Delete

	set global_list(actor_colorbar) -1
    }

    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  View fiber bundle
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_create_fiber_glyph { index } {

    set field(0) fractani
    set field(1) meandiff
    set field(2) distance
    set field(3) angle
    set field(4) roi
    set field(5) other
    set field(6) rgb

    ## create mapper
    set fiberMapper [ vtkPolyDataMapper [ pxvtable::vnewobj ] ]
    
    ## get current glyph
    set cur_glyph [ $widget_list(view_fiber_glyph) \
			index [ $widget_list(view_fiber_glyph) get ] ]
        
    ## get current fiber colormap filter    
    set filter [ $widget_list(view_fiber_filter) index [ $widget_list(view_fiber_filter) get ]]

    if { $cur_glyph == 1 } {
	set tube [ lindex $global_list(vtk_fiber_tube) $index ]
	
	$tube SetInput [ lindex $global_list(vtk_fiber_polydata) $index ]
	$tube SetRadius $global_list(view_fiber_tuberadius)
	$tube SetNumberOfSides 6
	$tube Update
	
	$fiberMapper SetInput [ $tube GetOutput ]	
	$fiberMapper SetLookupTable [ $widget_list(view_fiberluk) GetLookupTable ]
	$fiberMapper UseLookupTableScalarRangeOn
	$fiberMapper SetScalarModeToUsePointFieldData

	if { [ [ [ $tube GetOutput ] GetPointData ] GetArray $field($filter) ] != "" } {
	    $fiberMapper ColorByArrayComponent $field($filter) 0
	}
	
	if { $global_list(view_fiber_luk) } {	    
	    $fiberMapper ScalarVisibilityOn
	} else {
	    $fiberMapper ScalarVisibilityOff
	}

    } 

    if { [expr $cur_glyph == 0 || $cur_glyph == 2 ] } {
	
	set polydata [ lindex $global_list(vtk_fiber_polydata) $index ]

	$fiberMapper SetInput $polydata
	$fiberMapper SetLookupTable [ $widget_list(view_fiberluk) GetLookupTable ]
	$fiberMapper UseLookupTableScalarRangeOn
	$fiberMapper SetScalarModeToUsePointFieldData
	
	if { [ [ $polydata GetPointData ] GetArray $field($filter) ] != "" } {
	    $fiberMapper ColorByArrayComponent $field($filter) 0
	}

	if { $global_list(view_fiber_luk) } {	    
	    $fiberMapper ScalarVisibilityOn
	} else {
	    $fiberMapper ScalarVisibilityOff
	}
    }


    ## create actor
    set fiberActor [ vtkActor [ pxvtable::vnewobj ] ]
    $fiberActor SetMapper $fiberMapper
    
    if { $cur_glyph == 2 } {
	[ $fiberActor GetProperty ] SetRepresentationToPoints
	[ $fiberActor GetProperty ] SetPointSize $global_list(view_fiber_pointsize)
    }
    
    $fiberMapper Delete
    
    ## convert tk color spec to vtk
    set color [ lindex $global_list(fiber_color_list) $index ]
    set red "0x[string range $color 1 2]"
    set green "0x[string range $color 3 4]"
    set blue "0x[string range $color 5 6]"
    set red [ expr [ format "%d" $red ] / 255.0 ]
    set green [ expr [ format "%d" $green ] / 255.0 ]
    set blue [ expr [ format "%d" $blue ] / 255.0 ]
    
    [$fiberActor GetProperty] SetColor $red $green $blue
    
    return $fiberActor
}

#-------------------------------------------------------------------------------------------
#  View fibers
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_create_fibers { } {    

    set num [ llength $global_list(fiber_list) ]
    
    for {set j 0} {$j < $num} {incr j} {
	
	set actor [ view_create_fiber_glyph $j ]
	
	lappend global_list(actor_fiber) $actor
	
	## add actor if to be displayed
	if { [ lindex $global_list(fiber_disp_list) $j ] == 1 } { 
	    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] AddActor $actor
	}
    }
    
    [ $parent GetViewer ] UpdateDisplay
    
}

#-------------------------------------------------------------------------------------------
#  View colorbar
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_create_colorbar { } {
        
    set title [ $widget_list(view_fiber_filter) get ]
    if { $title == "Input map" } {
	set title $global_list(view_colorbar_label)
    }

    set bar [ vtkScalarBarActor [ pxvtable::vnewobj ] ]
    $bar SetLookupTable [ $widget_list(view_fiberluk) GetLookupTable ]
    
    $bar SetTitle $title
    [$bar GetPositionCoordinate] SetCoordinateSystemToNormalizedViewport
    [$bar GetPositionCoordinate] SetValue 0.1 0.01
    $bar SetOrientationToHorizontal
    $bar SetWidth 0.8
    $bar SetHeight 0.12
    
    set global_list(actor_colorbar) $bar
    
    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] AddActor2D $bar    
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in seed type
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::track_seedtype_cb { } {

    set state(0) disabled; set state(1) normal    
    set global_list(track_seedtype) \
	[ $widget_list(track_seedtype) index [ $widget_list(track_seedtype) get ] ]

    if { [expr $global_list(track_seedtype) < 2 || $global_list(track_seedtype) == 3 ]} {
	$widget_list(track_seedwidth) configure -state disabled
	$widget_list(track_seedheight) configure -state disabled
	$widget_list(track_seeddepth) configure -state disabled
	if { $global_list(track_seedtype) == 1 } {
	    $widget_list(track_seeddens) configure -state normal
	} else {
	    $widget_list(track_seeddens) configure -state disabled
	}
    } else {
	$widget_list(track_seedwidth) configure -state normal
	$widget_list(track_seedheight) configure -state normal
	$widget_list(track_seeddepth) configure -state normal
	$widget_list(track_seeddens) configure -state normal
    }
    
    if { $global_list(track_seedtype) == 1 } {
	$widget_list(track_seedregion) configure -state normal
    } else {
	$widget_list(track_seedregion) configure -state disabled	
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in region filtering option
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::track_regionfilter_cb { } {

    set state(1) normal
    set state(0) disabled

    $widget_list(track_regionnumber) configure -state \
	$state($global_list(track_regionfilter))
    
    $widget_list(track_clipping) configure -state \
	$state($global_list(track_regionfilter))
    
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in color schemes
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_directionscheme_cb { } {
    set global_list(view_direction_scheme) \
	[ $widget_list(view_direction_scheme) index [ $widget_list(view_direction_scheme) get ] ]
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in integration
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::track_integration_cb { } {
    set global_list(track_integration) \
	[ $widget_list(track_integration) index [ $widget_list(track_integration) get ] ]
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in eigenvector
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::track_eigenvector_cb { } {
    set global_list(track_eigenvector) \
	[ $widget_list(track_eigenvector) index [ $widget_list(track_eigenvector) get ] ]
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon an crosshair update
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_update_crosshair_cb { args } {

    set mousebutton [ lindex $args 1 ]
    set mousestate  [ lindex $args 2 ]
    
    ## mousebutton - 1: left, 2:middle, 3:right
    ## mousestate - 0:down, 1:move, 2:up
    
    ## get slice numbers
    set levels [ [ $parent GetViewer ] GetLastClickedPoint ] 
    if { [ llength $levels ] < 3 } {
	return
    }

    set cx [ lindex $levels 0 ]
    set cy [ lindex $levels 1 ]
    set cz [ lindex $levels 2 ]

    set ox [ lindex $global_list(track_lastpoint) 0 ]
    set oy [ lindex $global_list(track_lastpoint) 1 ]
    set oz [ lindex $global_list(track_lastpoint) 2 ]

    ## we are not under rotation
    if { $cx != -1 } {
	
	## puts "mouse state: $mousestate"

	set changedpoint 0
	if {  $cx != $ox || $cy != $oy || $cz != $oz   } {
	    set changedpoint 1
	}

	if { $mousestate == 0 && $changedpoint == 1 && $mousebutton == 4 } {

	    # create a new fiber if needed 
	    # not the same seed point
	    fiber_explore $cx $cy $cz

	    ## show fibers
	    view_update_display

	    ## record last point
	    set global_list(track_lastpoint) $cx
	    lappend global_list(track_lastpoint) $cy
	    lappend global_list(track_lastpoint) $cz	    
	} 
    }

    ## if you right-click, delete the fiber
    if { $mousebutton == 3 && $mousestate == 0 } {
	set index [expr [ llength $global_list(fiber_list) ] -1]
	if { $index >=0 } {
	    fiber_remove $index
	}
    }

}

#####
##### XENIOS LOOK BELOW FOR MOUSECALLBACK!
#####

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Toggles display auto-update
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_toggle_trackexplore { } {
    
    if { $global_list(track_explore) } {
	
	## make fibers show up -- will be deprecated eventually
	set global_list(view_fiber) 1

	## get slice numbers and set default last point
	set levels [ [ [ $parent GetViewer ] GetOrthoImageSlice ] GetLevels ]
	set global_list(track_lastpoint) $levels

	## set up mouse callback
        eval "$parent configure -mousecallback \"$this view_update_crosshair_cb\""
	
	set appname "$global_list(appname) $version \[Exploration\]"

	view_update_display 
	
    } else {
	
	## disable mouse callback
	eval "$parent configure -mousecallback 0"
	
	set appname "$global_list(appname) $version"
    }

    SetTitle $appname
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon an update button press
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_update_display_cb { } { 
    
    view_clear_display
    view_update_display
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon a change in bundle radius
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_tuberadius_cb { } {
    
    foreach item $global_list(vtk_fiber_tube) {
	$item SetRadius $global_list(view_fiber_tuberadius)
    }
    
    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon a change in point size
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_pointsize_cb { } {
    
    foreach actor $global_list(actor_fiber) {
	[ $actor GetProperty ] SetPointSize $global_list(view_fiber_pointsize)
    }
    
    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon copy eigen colormap 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_cmapresetbtn_cb { } {
    
    ## current fiber filter
    $widget_list(view_fiberluk) SetHueRange 0.5 0.0
    $widget_list(view_fiberluk) Update

}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon loading eigen colormap 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_cmaploadbtn_cb { } {   

    $widget_list(view_fiberluk) Load
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon saving eigen colormap 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_cmapsavebtn_cb { } {   

    $widget_list(view_fiberluk) SaveAs
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in colormap for fibers
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_colormap_cb { } {
    
    foreach \
	actor $global_list(actor_fiber) {
	    
	    ## create mapper
	    set fiberMapper [ $actor GetMapper ]

	    if { $global_list(view_fiber_luk) } {
		$fiberMapper ScalarVisibilityOn
	    } else {
		$fiberMapper ScalarVisibilityOff
	    }
	}
    
      [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon a change in bundle glyph
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_glyph_cb { } {

    set field(0) fractani
    set field(1) meandiff
    set field(2) distance
    set field(3) angle
    set field(4) roi
    set field(5) other
    set field(6) rgb

    ## get current fiber colormap filter    
    set filter [ $widget_list(view_fiber_filter) index [ $widget_list(view_fiber_filter) get ]]

    ## get current glyph
    set cur_glyph [ $widget_list(view_fiber_glyph) \
			index [ $widget_list(view_fiber_glyph) get ] ]
    
    foreach \
	actor $global_list(actor_fiber) \
	tube $global_list(vtk_fiber_tube) \
	polydata $global_list(vtk_fiber_polydata) {
	    
	    ## create mapper
	    set fiberMapper [ $actor GetMapper ]
	    
	    if { $cur_glyph == 1 } {
		
		$tube SetInput $polydata
		$tube SetRadius $global_list(view_fiber_tuberadius)
		$tube SetNumberOfSides 6
		$tube Update
		
		if { [ [ [ $tube GetOutput ] GetPointData ] GetArray $field($filter) ] != "" } {
		    $fiberMapper ColorByArrayComponent $field($filter) 0
		}

		$fiberMapper SetInput [ $tube GetOutput ]
		if { $global_list(view_fiber_luk) } {
		    $fiberMapper ScalarVisibilityOn
		} else {
		    $fiberMapper ScalarVisibilityOff
		}
		
	    } else {
		
		if { [ [ $polydata GetPointData ] GetArray $field($filter) ] != "" } {
		    $fiberMapper ColorByArrayComponent $field($filter) 0
		}

		$fiberMapper SetInput $polydata
		if { $global_list(view_fiber_luk) } {
		    $fiberMapper ScalarVisibilityOn
		} else {
		    $fiberMapper ScalarVisibilityOff
		}
	    }
	    
	    if { $cur_glyph == 2 } {
		[ $actor GetProperty ] SetRepresentationToPoints
		[ $actor GetProperty ] SetPointSize $global_list(view_fiber_pointsize)
	    } else {
		[ $actor GetProperty ] SetRepresentationToSurface 
	    }

	}
    
    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in fiber filter selection
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_fiber_filter_cb { } {

    set field(0) fractani
    set field(1) meandiff
    set field(2) distance
    set field(3) angle
    set field(4) roi
    set field(5) other
    set field(6) rgb

    ## current fiber colormap filter    
    set filter [ $widget_list(view_fiber_filter) index [ $widget_list(view_fiber_filter) get ]]
    
    foreach \
	actor $global_list(actor_fiber) {
	    
	    ## create mapper
	    set fiberMapper [ $actor GetMapper ]	   	    
	    $fiberMapper SetScalarModeToUsePointFieldData
	    
	    if { [ [ [ $fiberMapper GetInput ] GetPointData ] GetArray $field($filter) ] != "" } {
		$fiberMapper ColorByArrayComponent $field($filter) 0
	    }

	}
    
    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  Toggle On/Off fiber visualization
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::view_toggle_fiber { } {
    
    if { $global_list(view_fiber) } {	
	view_create_fibers	
    } else { 
	foreach \
	    actor $global_list(actor_fiber) \
	    disp $global_list(fiber_disp_list) {
		if { $disp == 1 } {
		    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] RemoveActor $actor
		}
		$actor Delete	    
	    }
	set global_list(actor_fiber) {}
    }

    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon a change in bundle color
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_color_cb { } {

    set new_color [ tk_chooseColor -initialcolor $global_list(fiber_color) ]
    
    if { [ llength $new_color ] > 0 } {
	
	set global_list(fiber_color) $new_color
	set fiber_color [ image create photo -width 15 -height 15 ]
	$fiber_color put $global_list(fiber_color) -to 0 0 15 15	
	$widget_list(fiber_color) configure -image $fiber_color
	
	## get current selection 
	set sel_item [ $widget_list(fiber_list) getcurselection ]
	
	if { $sel_item != "" } {
	    
	    set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	    set global_list(fiber_color_list) \
		[ lreplace $global_list(fiber_color_list) $sel_index $sel_index $new_color ]
	    
	    ## convert tk color spec to vtk
	    set color $new_color
	    set red "0x[string range $color 1 2]"
	    set green "0x[string range $color 3 4]"
	    set blue "0x[string range $color 5 6]"
	    set red [ expr [ format "%d" $red ] / 255.0 ]
	    set green [ expr [ format "%d" $green ] / 255.0 ]
	    set blue [ expr [ format "%d" $blue ] / 255.0 ]
	    
	    set fiberActor [ lindex $global_list(actor_fiber) $sel_index ]
	    [$fiberActor GetProperty] SetColor $red $green $blue

	    [ $parent GetViewer ] UpdateDisplay	    
	    
	}
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon selection of bundles
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_selection_cb { } {
    
    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	## get current index
	set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	set sel_input [ lindex $global_list(fiber_list) $sel_index ]
	
	## get number of components
	set global_list(fiber_name) $sel_input
	set global_list(fiber_color) [ lindex $global_list(fiber_color_list) $sel_index ]
	set global_list(fiber_display) [ lindex $global_list(fiber_disp_list) $sel_index ]

	set fiber_color [ image create photo -width 15 -height 15 ]
	$fiber_color put $global_list(fiber_color) -to 0 0 15 15	
	$widget_list(fiber_color) configure -image $fiber_color
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in bundle name
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_name_cb { } {

    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	set new_name [ $widget_list(fiber_name) get ]
	
	## get current index
	set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	set global_list(fiber_list) \
	    [ lreplace $global_list(fiber_list) $sel_index $sel_index $new_name ]
	
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE CALLBACK: Invoked upon change in bundle display option
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_display_cb { } {

    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	## get current index
	set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	set global_list(fiber_disp_list) \
	    [ lreplace $global_list(fiber_disp_list) $sel_index $sel_index \
		  $global_list(fiber_display) ]
	
	set fiberActor [ lindex $global_list(actor_fiber) $sel_index ]
	
	if { $global_list(fiber_display) } {
	    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] AddActor $fiberActor
	} else {
	    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] RemoveActor $fiberActor
	}
	
	[ $parent GetViewer ] UpdateDisplay	    
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Add bundle with a given name
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_addwithname { name polydata } {

    ## insert new fiber set
    set num_fibers [ llength $global_list(fiber_list) ]
    set next_color [lindex $global_list(tkcolor_list) [expr int(fmod($num_fibers,6))]]
    
    set global_list(fiber_color) $next_color
    set global_list(fiber_display) 1

    set fiber_color [ image create photo -width 15 -height 15 ]
    $fiber_color put $global_list(fiber_color) -to 0 0 15 15	
    $widget_list(fiber_color) configure -image $fiber_color
    
    set global_list(fiber_name) $name
    lappend global_list(fiber_list) $global_list(fiber_name)
    lappend global_list(fiber_disp_list) $global_list(fiber_display)    
    lappend global_list(fiber_color_list) $global_list(fiber_color)
    lappend global_list(vtk_fiber_polydata) $polydata	    
    lappend global_list(vtk_fiber_tube) [ vtkTubeFilter [ pxvtable::vnewobj ] ]
 
    $widget_list(fiber_list) selection clear 0 end
    $widget_list(fiber_list) selection set $num_fibers $num_fibers

    return $num_fibers
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Add fiber
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_add { polydata } {

    ## insert new fiber
    set num_fibers [ llength $global_list(fiber_list) ]
    set name "Bundle_[expr $num_fibers + 1]"
    
    set index [ fiber_addwithname $name $polydata ]
    
    return $index
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Clear all fibers
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_clear { } {
    
    set num_fibers [ llength $global_list(fiber_list) ]
    for { set i 0 } { $i < $num_fibers } { incr i } { 
	fiber_remove 0
    }
    
    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PUBLIC: Remove fiber
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_remove { index } {

    if { $global_list(view_fiber) } {
	set display [ lindex $global_list(fiber_disp_list) $index ]
	if { $display } {
	    set actor [ lindex $global_list(actor_fiber) $index ]
	    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] RemoveActor $actor
	    $actor Delete
	}
    }
    
    set global_list(fiber_list) \
	[ lreplace $global_list(fiber_list) $index $index ]
    set global_list(fiber_color_list) \
	[ lreplace $global_list(fiber_color_list) $index $index ]
    set global_list(fiber_disp_list) \
	[ lreplace $global_list(fiber_disp_list) $index $index ]
    
    set polydata [ lindex $global_list(vtk_fiber_polydata) $index ]
    $polydata Delete
    
    set tube [ lindex $global_list(vtk_fiber_tube) $index ]
    $tube Delete

    set global_list(actor_fiber) \
	[ lreplace $global_list(actor_fiber) $index $index ]
    set global_list(vtk_fiber_tube) \
	[ lreplace $global_list(vtk_fiber_tube) $index $index ]
    set global_list(vtk_fiber_polydata) \
	[ lreplace $global_list(vtk_fiber_polydata) $index $index ]
    
    set num_fibers [ llength $global_list(fiber_list) ]
    
    if { $num_fibers > 0 } {
	
	$widget_list(fiber_list) selection clear 0 end
	
	if { $index < $num_fibers } {
	    $widget_list(fiber_list) selection set $index $index
	} else {
	    $widget_list(fiber_list) selection set \
		[expr $num_fibers -1] [expr $num_fibers -1]
	}
	
	fiber_selection_cb
	
    } else {
	
	set global_list(fiber_fname) ""
	set global_list(fiber_display) 1
	set global_list(fiber_name) ""
	set global_list(fiber_color) [ lindex $global_list(tkcolor_list) 0 ]
	set fiber_color [ image create photo -width 15 -height 15 ]
	$fiber_color put $global_list(fiber_color) -to 0 0 15 15	
	$widget_list(fiber_color) configure -image $fiber_color
    }

    [ $parent GetViewer ] UpdateDisplay
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Remove selected bundle
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_removeselected { } {
    
    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	## get current index
	set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	
	fiber_remove $sel_index
	
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: load fiber
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_load { } {
    
    set fname  [tk_getOpenFile -title "Select fiber file" -filetypes { {"Fibers" {.fib}}} -initialdir $global_list(result_fdir) -parent $basewidget ] 

    ## browse for filename
    if { $fname != "" } { 
	
	## open fiber file
	set fibr [ vtkXMLPolyDataReader [ pxvtable::vnewobj ] ]
	$this SetFilterCallbacks $fibr "Reading fiber..."
	$fibr SetFileName $fname
	$fibr Update
	
	set polydata [ vtkPolyData [ pxvtable::vnewobj ] ]
	$polydata ShallowCopy [ $fibr GetOutput ]
	
	$fibr Delete
	
	fiber_addwithname [ file root [ file tail $fname ] ] $polydata
	
	## keep fname
	set global_list(fiber_fname) $fname
	
	if { $global_list(view_fiber) } {	
	    view_create_fibers	
	} 
    }	
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: save individual fiber 
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_save { index name } {
    
    set polydata [ lindex $global_list(vtk_fiber_polydata) $index ]
    
    set fibw [ vtkXMLPolyDataWriter [ pxvtable::vnewobj ] ]
    $this SetFilterCallbacks $fibw "Saving fiber..."

    $fibw SetFileName $name
    $fibw SetInput $polydata
    $fibw Write	
    
    $fibw Delete    
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: save selected fiber
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_saveselected { } {
    
    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	## browse for directory
	set fdir [ tk_chooseDirectory -title " Select directory" -parent $basewidget -initialdir $global_list(result_fdir) ]
	
	## browse for directory
	if { $fdir != "" } {
	    	    
	    ## get current index
	    set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	    
	    set item [ lindex $global_list(fiber_list) $sel_index ]	
	    
	    fiber_save $sel_index [ file join $fdir "$item.fib" ]	
	}
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: save all fibers
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_saveall { } {
    
    ## browse for directory    
    set fdir [ tk_chooseDirectory -title " Select directory" -parent $basewidget -initialdir $global_list(result_fdir) ]

    ## browse for directory
    if { $fdir != "" } { 
	
	## get number of fiber bundles
	set num [ llength $global_list(fiber_list) ]
	
	## save all fibers
	for { set i 0 } { $i < $num } { incr i } {	
	    set item [ lindex $global_list(fiber_list) $i ]	
	    fiber_save $i [ file join $fdir "$item.fib" ]	
	}
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: Save statistics
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_savestatistics { } {

    set statfname $global_list(result_fpreffix)
    set initfname "${statfname}stat"
    set initfname [ file join $global_list(result_fdir) $initfname ]

    ## browse for filename
    set fname  [tk_getSaveFile -title "Save statistics"\
		    -filetypes {{"Text" {.txt*}}} \
		    -parent $basewidget \
		    -defaultextension ".txt" \
		    -initialfile $initfname ]  
    
    if { [ string length $fname ] < 1 } {
	return 0
    }
    
    $widget_list(stat_text) export $fname

}

#-------------------------------------------------------------------------------------------
#  PRIVATE: compute fiber statistics
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_beginstatistics { title overwrite } {   
    
    if { $overwrite } {
	$widget_list(stat_text) delete 1.0 end
    }    

    $widget_list(stat_text) insert \
	end "-----------------------+------------+------------+------------+------------+------------\n" even
    $widget_list(stat_text) insert \
	end "$title      MIN          MAX         MEAN        VARIANCE      STD DEV\n" odd
    $widget_list(stat_text) insert \
	end "-----------------------+------------+------------+------------+------------+------------\n" even
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: compute fiber statistics
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_addstatistics { title results tag } {   
    
    set min  [ format %8.4f [ lindex $results 0 ] ]
    set max  [ format %8.4f [ lindex $results 1 ] ]
    set mean [ format %8.4f [ lindex $results 2 ] ]
    set var  [ format %8.4f [ lindex $results 3 ] ]
    set dev  [ format %8.4f [ lindex $results 4 ] ]
    
    $widget_list(stat_text) insert end " $title |  $min  |  $max  |  $mean  |  $var  |  $dev  \n" $tag
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: compute fiber statistics
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_statistics { } {

    set FIBERS 3

    set fiber_image [ lindex $global_list(result_list) $FIBERS ] 

#    $widget_list(notebook) view "Statistics"
    
    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	## get current index
	set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	
	## name
	set item [ lindex $global_list(fiber_list) $sel_index ]	
	
	# compute fiber statistics
	set fstat [ vtkmpjTensorStreamlineStatistics [ pxvtable::vnewobj ] ]
	$fstat SetInput [ lindex $global_list(vtk_fiber_polydata) $sel_index ]
	$fstat Update
	
	set vol "n/a"
	set volmm "n/a"

	if { [ $fiber_image GetImageSize ] > 1 } {
	    
	    ## quantize fibers
	    set quant [ vtkmpjStreamlineQuantization [ pxvtable::vnewobj ] ]
	    $quant SetInput [ lindex $global_list(vtk_fiber_polydata) $sel_index ]
	    $quant SetImage [ $fiber_image GetImage ]
	    $quant SetMaskValue 1.0
	    $quant Update
	    
	    $fiber_image ShallowCopyImage [ $quant GetOutput ]

	    ## get volume 
	    set vol [ $quant GetNumberOfVoxels ]

	    ## get spacing information
	    scan [ [ $fiber_image GetImage ] GetSpacing ] "%f %f %f" spc0 spc1 spc2

	    ## calculate volume in mm
	    set volmm [ expr $vol * $spc0 * $spc1 * $spc2 ]
	    set volmm [ format %6.2f $volmm ]

	    $quant Delete
	}
	
	## Write out stats
	set textlist [  $widget_list(stat_text) component text ]
	$textlist tag configure even -background "\#e0e0ff"
	
	set strlen [ string length $item ]
	if { $strlen > 23 } {
	    set item [ string range $item 0 22 ]
	} else {
	    set spc [ string repeat " " [ expr (23 - $strlen) / 2 ] ] 
	    set item [ string range "$spc$item$spc" 0 22 ]
	}

	fiber_beginstatistics $item $global_list(stat_overwrite)

	set fa [ $fstat GetAnisotropy ]
	set tmpfa [ lindex $fa 3 ]
	if { $tmpfa >= 0 } {
	    set tmpfa [ expr sqrt($tmpfa) ]
	} else {
	    set tmpfa "sqrt($tmpfa)!"
	}
	lappend fa $tmpfa
	fiber_addstatistics "FRACTIONAL ANISOTROPY" $fa odd

	set md [ $fstat GetDiffusivity ]
	set tmpmd [ lindex $md 3 ]
	if { $tmpmd >= 0 } {
	    set tmpmd [ expr sqrt($tmpmd) ]
	} else {
	    set tmpmd "sqrt($tmpmd)!"
	}
	lappend md $tmpmd
	fiber_addstatistics "MEAN DIFFUSIVITY     " $md even	
	
	set dist [ $fstat GetDistance ]
	set tmpdist [ lindex $dist 3 ]
	if { $tmpdist >= 0 } {
	    set tmpdist [ expr sqrt($tmpdist) ]
	} else {
	    set tmpdist "sqrt($tmpdist)!"
	}
	lappend dist $tmpdist
	fiber_addstatistics "MEAN FIBER LENGTH    " $dist odd
	
	set angle [ $fstat GetAngle ]
	set tmpangle [ lindex $angle 3 ]
	if { $tmpangle >= 0 } {
	    set tmpangle [ expr sqrt($tmpangle) ]
	} else {
	    set tmpangle "sqrt($tmpangle)!"
	}
	lappend angle $tmpangle
	fiber_addstatistics "MEAN FIBER ANGLE     " $angle even	
	
	set other [ $fstat GetOther ]
	set tmpother [ lindex $other 3 ]
	if { $tmpother >= 0 } {
	    set tmpother [ expr sqrt($tmpother) ]
	} else {
	    set tmpother "sqrt($tmpother)!"
	}
	lappend other $tmpother
	fiber_addstatistics "MEAN INPUT MAP       " $other odd

	$widget_list(stat_text) insert \
	    end "-----------------------+------------+------------+------------+------------+------------\n" even

	set num [ $fstat GetNumberOfFibers ]

	$widget_list(stat_text) insert end " Fiber Count = $num, Volume = $vol ($volmm mm^3)\n" odd

	$fstat Delete
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: quantize fibers
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_quantize { } {

    set FIBERS 3

    set fiber_image [ lindex $global_list(result_list) $FIBERS ] 

    ## get current selection 
    set sel_item [ $widget_list(fiber_list) getcurselection ]
    
    if { $sel_item != "" } {
	
	## get current index
	set sel_index [ $widget_list(fiber_list) index [ lindex $sel_item 0 ] ] 
	
	## name
	set item [ lindex $global_list(fiber_list) $sel_index ]	

	## quantize fibers
	set quant [ vtkmpjStreamlineQuantization [ pxvtable::vnewobj ] ]
	$quant SetInput [ lindex $global_list(vtk_fiber_polydata) $sel_index ]
	$quant SetImage [ $fiber_image GetImage ]
	$quant SetMaskValue 1.0
	$quant Update

	$fiber_image ShallowCopyImage [ $quant GetOutput ]

	$quant Delete
    }
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: fiber explore
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_explore { cx cy cz } {
    
    set TENSOR 0
    set MASK 1
    set MAP 2
    set FIBERS 3
    
    set tensor [ lindex $global_list(result_list) $TENSOR ]
    set mask [ lindex $global_list(result_list) $MASK ]
    set map [ lindex $global_list(result_list) $MAP ]
    set fibers [ lindex $global_list(result_list) $FIBERS ] 

    if { [ $tensor GetImageSize ] == 1 } {
	
	tk_messageBox -type ok \
	    -parent $basewidget -title "Tensor" \
	    -message "You need a tensor image to start tracking!" -icon info

	return
    }
     
    if { [ $map GetImageSize ] == 1 } {
	
	## create dummy map
	$map CopyImageHeader [ $fibers GetImageHeader ]
	$map ShallowCopyImage [ $fibers GetImage ]
    }

    ## if tracking, one wants to see the fibers
    set global_list(view_fiber) 1

    WatchOn

    ## get origin and spacing information
    scan [ [ $tensor GetImage ] GetSpacing ] "%f %f %f" spc0 spc1 spc2
    scan [ [ $tensor GetImage ] GetOrigin ] "%f %f %f" org0 org1 org2

    ## initialize streamer
    set streamer [ vtkmpjTensorStreamline [ pxvtable::vnewobj ] ]
    $this SetFilterCallbacks $streamer "Fiber tracking..."
    $streamer SetInput [ $map GetImage ]
    $streamer SetTensor [ $tensor GetImage ]
    
    if { [ $mask GetImageSize ] > 1 } {
	$streamer SetMask [ $mask GetImage ]
    }
    
    ## honor only volume or single point seeds
    if { $global_list(track_seedtype) == 2 } {
	
	## volume	
	set ox [expr $cx * $spc0 + $org0 ]
	set oy [expr $cy * $spc1 + $org1 ]
	set oz [expr $cz * $spc2 + $org2 ]
	
	set width $global_list(track_seedwidth)
	set height $global_list(track_seedheight)
	set depth $global_list(track_seeddepth)
	set dens $global_list(track_seeddens)
	
	#	set numx [ expr int(1.0 / $res) * $width ]
	#	set numy [ expr int(1.0 / $res) * $height ]
	#	set numz [ expr int(1.0 / $res) * $depth ]
	
	set source [ vtkmpjImagePointSource [ pxvtable::vnewobj ] ]
	$source SetCenter $ox $oy $oz
	$source SetSize $width $height $depth
	$source SetOrigin $org0 $org1 $org2
	$source SetSpacing $spc0 $spc1 $spc2
	$source SetDensity $dens
	$source Update

	## assign source points
	$streamer SetSource [ $source GetOutput ]

	$source Delete

    } else {
	
	## single input point
	set ox [expr $cx * $spc0 + $org0 ]
	set oy [expr $cy * $spc1 + $org1 ]
	set oz [expr $cz * $spc2 + $org2 ]
	
	set source [ vtkPolyData [ pxvtable::vnewobj ] ] 
	set points [ vtkPoints [ pxvtable::vnewobj ] ]
	
	$points InsertNextPoint $ox $oy $oz 
	$source SetPoints $points
	
	$points Delete
	
	## assign source point
	$streamer SetSource $source

	$source Delete

    }
    
#    puts "num seed points: [ [ [ $streamer GetSource ] GetPoints ] GetNumberOfPoints ]"
    
    ## integrate fibers
    if { $global_list(track_flipping) } {
	$streamer SetFlipX $global_list(track_flipx)
	$streamer SetFlipY $global_list(track_flipy)
	$streamer SetFlipZ $global_list(track_flipz)
    }

    ## specify eigenvector
    if { $global_list(track_eigenvector) == 0 } { 
	$streamer IntegrateMajorEigenvector
    } elseif { $global_list(track_eigenvector) == 1 } {
	$streamer IntegrateMediumEigenvector
    } else {
	$streamer IntegrateMinorEigenvector
    }

    $streamer SetIntegrationDirectionToIntegrateBothDirections
    $streamer SetMaximumDistance $global_list(track_maxdist)
    $streamer SetMinimumDistance $global_list(track_mindist)
    $streamer SetIntegrationOrder $global_list(track_integration)
    $streamer SetMaximumAngle $global_list(track_maxangle)
    $streamer SetMinimumFA $global_list(track_minFA);
    $streamer SetMaximumFA $global_list(track_maxFA);
    $streamer SetMinimumMD $global_list(track_minMD);
    $streamer SetMaximumMD $global_list(track_maxMD);
    $streamer SetIntegrationStepLength $global_list(track_steplen)

    $streamer SetDirectionalColormap $global_list(track_dec)
    $streamer SetDirectionalColorScheme $global_list(view_direction_scheme)
    $streamer SetDirectionalColorSaturation $global_list(view_direction_saturation)
    $streamer SetDirectionalColorScaling $global_list(view_direction_scaling)
    $streamer SetDirectionalColorModulate $global_list(view_direction_modulate)
    $streamer SetDirectionalColorAlphaBlending $global_list(view_direction_alpha)
    
    $streamer Update
    
    ## create new fiber set
    set new_fiber [ vtkPolyData [ pxvtable::vnewobj ] ]

    ## filter fibers
    if { $global_list(track_regionfilter) } {	
	
	set strfilter [ vtkmpjTensorStreamlineRegionFilter [ pxvtable::vnewobj ] ]
	
	$strfilter SetInput [ $streamer GetOutput ]
	$strfilter SetRegionNumber1 $global_list(track_seedregion)
	$strfilter SetRegionNumber2 $global_list(track_regionnumber)
	$strfilter SetClipping $global_list(track_clipping)
	$strfilter Update
	
	$new_fiber ShallowCopy [ $strfilter GetOutput ]

	$strfilter Delete	

    } else {

	$new_fiber ShallowCopy [ $streamer GetOutput ]

    }

    $streamer Delete
    
    ## add new fiber bundle
    set index [ fiber_add $new_fiber ]
    
    set actor [ view_create_fiber_glyph $index ]
    lappend global_list(actor_fiber) $actor
	
    ## add actor if to be displayed
    if { [ lindex $global_list(fiber_disp_list) $index ] == 1 } { 
	[ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] AddActor $actor
    }

    WatchOff
}

#-------------------------------------------------------------------------------------------
#  PRIVATE: fiber track
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::fiber_track { } {

    set TENSOR 0
    set MASK 1
    set MAP 2
    set FIBERS 3
    
    set tensor [ lindex $global_list(result_list) $TENSOR ]
    set mask [ lindex $global_list(result_list) $MASK ]
    set map [ lindex $global_list(result_list) $MAP ]
    set fibers [ lindex $global_list(result_list) $FIBERS ] 

    if { [ $tensor GetImageSize ] == 1 } {
	
	tk_messageBox -type ok \
	    -parent $basewidget -title "Tensor" \
	    -message "You need a tensor image to start tracking!" -icon info

	return
    }
     
    if { [ $map GetImageSize ] == 1 } {
	
	## create dummy map
	$map CopyImageHeader [ $fibers GetImageHeader ]
	$map ShallowCopyImage [ $fibers GetImage ]
    }

    ## if tracking, one wants to see the fibers
    set global_list(view_fiber) 1

    WatchOn

    ## get slice numbers
    set levels [ [ [ $parent GetViewer ] GetOrthoImageSlice ] GetLevels ]
    set cx [ lindex $levels 0 ]
    set cy [ lindex $levels 1 ]
    set cz [ lindex $levels 2 ]

    ## get origin and spacing information
    scan [ [ $tensor GetImage ] GetSpacing ] "%f %f %f" spc0 spc1 spc2
    scan [ [ $tensor GetImage ] GetOrigin ] "%f %f %f" org0 org1 org2

    ## initialize streamer
    set streamer [ vtkmpjTensorStreamline [ pxvtable::vnewobj ] ]
    $this SetFilterCallbacks $streamer "Fiber tracking..."
    $streamer SetInput [ $map GetImage ]
    $streamer SetTensor [ $tensor GetImage ]
    
    if { [ $mask GetImageSize ] > 1 } {
	$streamer SetMask [ $mask GetImage ]
    }
    
    if { $global_list(track_seedtype) == 0 } {

	## single input point
	set ox [expr $cx * $spc0 + $org0 ]
	set oy [expr $cy * $spc1 + $org1 ]
	set oz [expr $cz * $spc2 + $org2 ]
	
	set source [ vtkPolyData [ pxvtable::vnewobj ] ] 
	set points [ vtkPoints [ pxvtable::vnewobj ] ]
	
	$points InsertNextPoint $ox $oy $oz 
	$source SetPoints $points
	
	$points Delete
	
	## assign source point
	$streamer SetSource $source

	$source Delete

    } elseif { $global_list(track_seedtype) == 1 } {
	
	## region 
	set region $global_list(track_seedregion)
	
	set imthr [ vtkImageThreshold [ pxvtable::vnewobj ] ]
	$imthr SetInput [ $mask GetImage ]
	$imthr ThresholdBetween $region $region
	$imthr SetInValue 1.0
	$imthr SetOutValue 0.0
	$imthr SetOutputScalarTypeToUnsignedChar
	$imthr Update
	
 	set dens $global_list(track_seeddens)

	set source [ vtkmpjImagePointSource [ pxvtable::vnewobj ] ]
	$source SetMask [ $imthr GetOutput ]
	$source SetOrigin $org0 $org1 $org2
	$source SetSpacing $spc0 $spc1 $spc2
	$source SetDensity $dens
	$source Update
	
 	## assign source points
	$streamer SetSource [ $source GetOutput ]
	$source Delete
	
	#	set impts [ vtkThresholdPoints [ pxvtable::vnewobj ] ]
	#	$impts SetInput [ $imthr GetOutput ]
	#	$impts ThresholdByUpper 1.0
	#	$impts Update    
	
	$imthr Delete
	
	## assign region source points
#	$streamer SetSource [ $impts GetOutput ]
#	$impts Delete
	
    } elseif { $global_list(track_seedtype) == 2 } {
	
	## volume	
	set ox [expr $cx * $spc0 + $org0 ]
	set oy [expr $cy * $spc1 + $org1 ]
	set oz [expr $cz * $spc2 + $org2 ]
	
	set width $global_list(track_seedwidth)
	set height $global_list(track_seedheight)
	set depth $global_list(track_seeddepth)
	set dens $global_list(track_seeddens)
	
	#	set numx [ expr int(1.0 / $res) * $width ]
	#	set numy [ expr int(1.0 / $res) * $height ]
	#	set numz [ expr int(1.0 / $res) * $depth ]
	
	set source [ vtkmpjImagePointSource [ pxvtable::vnewobj ] ]
	$source SetCenter $ox $oy $oz
	$source SetSize $width $height $depth
	$source SetOrigin $org0 $org1 $org2
	$source SetSpacing $spc0 $spc1 $spc2
	$source SetDensity $dens
	$source Update

	## assign source points
	$streamer SetSource [ $source GetOutput ]

	$source Delete

    } elseif { $global_list(track_seedtype) == 3 } {
	
	## landmark control
	set landmarkcontrol [ [ $parent GetLandmarkControl ] GetLandmarkControl ]
	
	set lmarks [ $landmarkcontrol GetCollection -1 ]	
	$lmarks Compact
	set numlmarks [ $lmarks GetNumPoints ]
	
	if { $numlmarks > 0 } {
	    
	    set pts [ vtkPolyData [ pxvtable::vnewobj ] ]
	    $pts SetPoints [ $lmarks GetPoints ]
	    
	    $streamer SetSource $pts
	    
	    $pts Delete
	}
    }
    
#    puts "num seed points: [ [ [ $streamer GetSource ] GetPoints ] GetNumberOfPoints ]"
    
    ## integrate fibers
    if { $global_list(track_flipping) } {
	$streamer SetFlipX $global_list(track_flipx)
	$streamer SetFlipY $global_list(track_flipy)
	$streamer SetFlipZ $global_list(track_flipz)
    }

    ## specify eigenvector
    if { $global_list(track_eigenvector) == 0 } { 
	$streamer IntegrateMajorEigenvector
    } elseif { $global_list(track_eigenvector) == 1 } {
	$streamer IntegrateMediumEigenvector
    } else {
	$streamer IntegrateMinorEigenvector
    }

    $streamer SetIntegrationDirectionToIntegrateBothDirections
    $streamer SetMaximumDistance $global_list(track_maxdist)
    $streamer SetMinimumDistance $global_list(track_mindist)
    $streamer SetIntegrationOrder $global_list(track_integration)
    $streamer SetMaximumAngle $global_list(track_maxangle)
    $streamer SetMinimumFA $global_list(track_minFA);
    $streamer SetMaximumFA $global_list(track_maxFA);
    $streamer SetMinimumMD $global_list(track_minMD);
    $streamer SetMaximumMD $global_list(track_maxMD);
    $streamer SetIntegrationStepLength $global_list(track_steplen)

    $streamer SetDirectionalColormap $global_list(track_dec)
    $streamer SetDirectionalColorScheme $global_list(view_direction_scheme)
    $streamer SetDirectionalColorSaturation $global_list(view_direction_saturation)
    $streamer SetDirectionalColorScaling $global_list(view_direction_scaling)
    $streamer SetDirectionalColorModulate $global_list(view_direction_modulate)
    $streamer SetDirectionalColorAlphaBlending $global_list(view_direction_alpha)
    
    $streamer Update
    
    ## create new fiber set
    set new_fiber [ vtkPolyData [ pxvtable::vnewobj ] ]

    ## filter fibers
    if { $global_list(track_regionfilter) } {	
	
	set strfilter [ vtkmpjTensorStreamlineRegionFilter [ pxvtable::vnewobj ] ]
	
	$strfilter SetInput [ $streamer GetOutput ]
	$strfilter SetRegionNumber1 $global_list(track_seedregion)
	$strfilter SetRegionNumber2 $global_list(track_regionnumber)
	$strfilter SetClipping $global_list(track_clipping)
	$strfilter Update
	
	$new_fiber ShallowCopy [ $strfilter GetOutput ]

	$strfilter Delete	

    } else {

	$new_fiber ShallowCopy [ $streamer GetOutput ]

    }

    $streamer Delete
    
    set index [ fiber_add $new_fiber ]
    
#    puts "points: [[$new_fiber GetPoints] GetNumberOfPoints]"

    ## display new bundle
    if { $global_list(view_fiber) } {	
	
	set actor [ view_create_fiber_glyph $index ]
	lappend global_list(actor_fiber) $actor
	
	## add actor if to be displayed
	if { [ lindex $global_list(fiber_disp_list) $index ] == 1 } { 
	    [ [ [ $parent GetViewer ] GetRenderer 3 ] GetRenderer ] AddActor $actor
	}

	[ $parent GetViewer ] UpdateDisplay
    }

    ## compute statistics
    if { $global_list(track_autostat) } {
	fiber_statistics
    }

    
    WatchOff
}

#-------------------------------------------------------------------------------------------
#  Create results control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateInputControl { base } {   

    iwidgets::labeledframe $base.frame1 \
	-labelpos nw  \
	-labeltext "Tensor image"
    pack $base.frame1 -fill both -expand f -pady 5
    
    set frame1 [ $base.frame1 childsite ]
    
    set subframe0 [ frame $frame1.subframe0 ]
    pack $subframe0 -side top -fill both -expand f -pady 0
    
    set tensor_fname [ iwidgets::entryfield $subframe0.tensor_fname \
			   -textvariable [ itcl::scope global_list(tensor_fname) ] \
			   -labeltext "Filename:" ] 
    pack $tensor_fname -side top -expand t -fill x -padx 5 -pady 5
    set widget_list(tensor_fname) $tensor_fname
    
    set tensor_load [ iwidgets::buttonbox $subframe0.tensor_load -orient horizontal -padx 2 ]
    $tensor_load add load -text "Load..." -command [ itcl::code $this loadtensor ] 
    $tensor_load add disp -text "Display" -command [ itcl::code $this DisplayResult 0 ]
    pack $tensor_load -side left
    set widget_list(tensor_load) $tensor_load
    
    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Region of interest mask"
    pack $base.frame0 -fill both -expand f -pady 5
    
    set frame0 [ $base.frame0 childsite ]
  
    iwidgets::labeledframe $base.frame2 \
	-labelpos nw  \
	-labeltext "Map for analysis"
    pack $base.frame2 -fill both -expand f -pady 5
    
    set frame2 [ $base.frame2 childsite ]
    
    set subframe0 [ frame $frame2.subframe0 ]
    pack $subframe0 -side top -fill x -expand f -pady 0
    
    set map_fname [ iwidgets::entryfield $subframe0.map_fname \
			-textvariable [ itcl::scope global_list(map_fname) ] \
			-labeltext "Filename:" ] 
    pack $map_fname -side top -expand t -fill x -padx 5 -pady 5
    set widget_list(map_fname) $map_fname
    
    set map_load [ iwidgets::buttonbox $subframe0.map_load -orient horizontal -padx 2 ]
    $map_load add load -text "Load..." -command [ itcl::code $this loadmap ] 
    $map_load add disp -text "Display" -command [ itcl::code $this DisplayResult 2 ]
    pack $map_load -side left 
    set widget_list(map_load) $map_load

    set subframe1 [ frame $frame0.subframe1 ]
    pack $subframe1 -side top -fill both -expand f -pady 0
    
    set mask_fname [ iwidgets::entryfield $subframe1.mask_fname \
			 -textvariable [ itcl::scope global_list(mask_fname) ] \
			 -labeltext "Filename:" ] 
    pack $mask_fname -side top -expand t -fill x -padx 5 -pady 5
    set widget_list(mask_fname) $mask_fname
    
    set mask_load [ iwidgets::buttonbox $subframe1.mask_load -orient horizontal -padx 2 ]
    $mask_load add load -text "Load..." -command [ itcl::code $this loadmask ]    
    $mask_load add disp -text "Display" -command [ itcl::code $this DisplayResult 1 ]
    pack $mask_load -side left 
    set widget_list(mask_load) $mask_load
    
    set mask_nreg [ iwidgets::spinint $subframe1.mask_nreg \
			-labeltext "Number of regions in mask:" \
			-fixed 4 -width 4 -range {1 1000} -step 1 \
			-textvariable [ itcl::scope global_list(mask_nreg) ] ]
    pack $mask_nreg -side right -pady 5 -padx 5
    set widget_list(mask_nreg) $mask_nreg

  
}

#-------------------------------------------------------------------------------------------
#  Create results control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateResultsControl { base } {   

    set color(0) \#dcdcdc
    set color(1) black

    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Results"
    pack $base.frame0 -fill both -expand t 
    
    set frame0 [ $base.frame0 childsite ]
    
    set subframe0 [ frame $frame0.subframe0 ]
    pack $subframe0 -fill both -expand t
    
    iwidgets::scrolledlistbox $subframe0.resultlist \
	-hscrollmode dynamic \
	-selectmode extended \
	-dblclickcommand [ itcl::code $this toggleresult ] \
	-visibleitems 40x10 
    
    pack $subframe0.resultlist -side left -fill both -expand t -padx 2 -pady 2
    set widget_list(result_list) $subframe0.resultlist
    
    foreach item $global_list(result_title) {
	set enable [ lindex $item 2 ]
     	$widget_list(result_list) insert end "[lindex $item 0]" 
	$widget_list(result_list) itemconfigure end -foreground $color($enable)
    }

    set subframe1 [ frame $subframe0.subframe1 ]
    pack $subframe1 -side right -fill both -padx 0
    
    set resultbbox [ iwidgets::buttonbox $subframe1.input -orient vertical ]
    $resultbbox add display -text "Display" -command [ itcl::code $this displayresult ]
    $resultbbox add save -text "Save..." -command [ itcl::code $this saveresult ]
    $resultbbox add saveall -text "Save all" -command [ itcl::code $this saveallresults ]    
    $resultbbox add mark -text "On/Off" -command [ itcl::code $this toggleresult ]
    pack $resultbbox -side top -fill x -expand t -padx 0
    set widget_list(result_buttonbox) $resultbbox

    set subframe2 [ frame $frame0.subframe2 ]
    pack $subframe2 -side top -fill x -padx 2 -pady 5

    set result_fpreffix [ iwidgets::entryfield $subframe2.result_fpreffix \
			      -textvariable [ itcl::scope global_list(result_fpreffix) ] \
			      -labeltext "Preffix:" ]
    pack $result_fpreffix -side left -fill x -expand t -padx 5 -pady 2
    set widget_list(result_fpreffix) $result_fpreffix    
}

#-------------------------------------------------------------------------------------------
#  Create directionality encoding display control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateDirectionControl { base } {
    
    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Directionally encoded colormaps"
    pack $base.frame0 -fill both -expand f
    
    set frame0 [ $base.frame0 childsite ]

    set subframe0 [ frame $frame0.subframe0 ]
    pack $subframe0 -fill x -expand f -padx 5 -pady 5

    ## color encoding
    set track_dec [ checkbutton $subframe0.track_dec \
			-text "Enable fiber direction colormaps" \
			-variable [ itcl::scope global_list(track_dec) ] ]
    pack $track_dec -side left -padx 5 -pady 5      
    
    set subframe1 [ frame $frame0.subframe1 ]
    pack $subframe1 -fill both -expand f -padx 5      
    
    set view_direction_modulate [ checkbutton $subframe1.view_direction_modulate \
				      -text "Modulate intensity by fractional anisotropy" \
				      -variable [ itcl::scope global_list(view_direction_modulate) ] ]
    pack $view_direction_modulate -side left -padx 5 -pady 5

    set view_direction_alpha [ checkbutton $subframe1.view_direction_alpha \
				   -text "Enable transparency" \
				   -variable [ itcl::scope global_list(view_direction_alpha) ] ]
    pack $view_direction_alpha -side right -padx 5 -pady 5

    set subframe2 [ frame $frame0.subframe2 ]
    pack $subframe2 -fill x -expand f -padx 5 -pady 5
  
    set view_direction_scheme [ iwidgets::optionmenu $subframe2.view_direction_scheme \
				    -labeltext "Color scheme:" \
				    -command [ itcl::code $this view_directionscheme_cb ] ]
    set widget_list(view_direction_scheme) $view_direction_scheme
    
    $view_direction_scheme insert 0 "Absolute value"
    $view_direction_scheme insert 1 "Rotational symmetry"
    $view_direction_scheme insert 2 "No symmetry"
    $view_direction_scheme insert 3 "Mirror symmetry"

    pack $view_direction_scheme -side left -padx 5
    
    $view_direction_scheme select 0    

    set subframe4 [ frame $frame0.subframe4 ]
    pack $subframe4 -fill x -expand f -padx 5 -pady 5
    
    iwidgets::entryfield $subframe4.view_direction_satscale \
	-width 5 \
	-validate real \
	-labeltext "Saturation factor:" \
	-textvariable [ itcl::scope global_list(view_direction_saturation) ]
    pack $subframe4.view_direction_satscale -side left -padx 5 -pady 5
    
    iwidgets::entryfield $subframe4.view_direction_scaling \
	-width 5 \
	-validate real \
	-labeltext "Brightness:" \
	-textvariable [ itcl::scope global_list(view_direction_scaling) ]
    pack $subframe4.view_direction_scaling -side left -padx 5 -pady 5

  
}

#-------------------------------------------------------------------------------------------
#  Create fiber tracking control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateTrackingControl { base } {

    label $base.dummy -bg black -fg white -text "In Exploration mode, use Shift-LeftBtn to track; RightBtn to remove bundle"
    pack $base.dummy -side top -fill x -expand t

    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Seed points"
    pack $base.frame0 -fill both -expand f 
    
    set frame0 [ $base.frame0 childsite ]

    set subframe0 [ frame $frame0.subframe0 ]
    pack $subframe0 -fill both -expand f
    
    set track_seedtype [ iwidgets::optionmenu $subframe0.track_seedtype \
			     -command [ itcl::code $this track_seedtype_cb ] \
			     -labeltext "Seed from:" ]
    pack $track_seedtype -side left -padx 5 -pady 5
    $track_seedtype insert end "Single point"
    $track_seedtype insert end "Region"
    $track_seedtype insert end "Volume"
    $track_seedtype insert end "Landmark control"
    set widget_list(track_seedtype) $track_seedtype

    set track_seedregion [ iwidgets::spinint $subframe0.track_seedregion \
			       -labeltext "Region number:" \
			       -fixed 4 -width 4 -range {1 1000} -step 1 \
			       -textvariable \
			       [ itcl::scope global_list(track_seedregion) ] ]
    pack $track_seedregion -side left -pady 5 -padx 2
    set widget_list(track_seedregion) $track_seedregion  
    $widget_list(track_seedregion) configure -state disabled
    
    set subframe1 [ frame $frame0.subframe1 ]
    pack $subframe1 -fill both -expand f
    
    set track_seedwidth [ iwidgets::entryfield $subframe1.track_seedwidth \
			      -labeltext "Width:" \
			      -validate integer \
			      -width 4 \
			      -state disabled \
			      -textvariable [ itcl::scope global_list(track_seedwidth) ] ]
    pack $subframe1.track_seedwidth -side left -padx 5 -pady 5 
    set widget_list(track_seedwidth) $track_seedwidth
    
    set track_seedheight [ iwidgets::entryfield $subframe1.track_seedheight \
			       -labeltext "Height:" \
			       -validate integer \
			       -width 4 \
			       -state disabled \
			       -textvariable [ itcl::scope global_list(track_seedheight) ] ]
    pack $subframe1.track_seedheight -side left -padx 5 -pady 5
    set widget_list(track_seedheight) $track_seedheight
    
    set track_seeddepth [ iwidgets::entryfield $subframe1.track_seeddepth \
			      -labeltext "Depth:" \
			      -validate integer \
			      -width 4 \
			      -state disabled \
			      -textvariable [ itcl::scope global_list(track_seeddepth) ] ]
    pack $subframe1.track_seeddepth -side left -padx 5 -pady 5 
    set widget_list(track_seeddepth) $track_seeddepth
    
    set track_seeddens [ iwidgets::entryfield $subframe1.track_seeddens \
			    -labeltext "Density:" \
			    -validate real \
			    -width 6 \
			    -state disabled \
			    -textvariable [ itcl::scope global_list(track_seeddens) ] ]
    pack $subframe1.track_seeddens -side left -padx 5 -pady 5
    set widget_list(track_seeddens) $track_seeddens

    iwidgets::labeledframe $base.frame1 \
	-labelpos nw  \
	-labeltext "Integration parameters"
    pack $base.frame1 -fill both -expand f
    
    set frame1 [ $base.frame1 childsite ]
    
    set subframe2 [ frame $frame1.subframe2 ]
    pack $subframe2 -fill both -expand f
    
    set integration [ iwidgets::optionmenu $subframe2.integration \
			  -command [ itcl::code $this track_integration_cb ] \
			  -labeltext "Type:" ]	
    pack $integration -side left -padx 2 -pady 5
    $integration insert end "Runge-Kutta 2"
    $integration insert end "Runge-Kutta 4"
    set widget_list(track_integration) $integration
    $widget_list(track_integration) select $global_list(track_integration)
    
    iwidgets::entryfield $subframe2.track_steplen \
	-width 5 \
	-validate real \
	-labeltext "Step length:" \
	-textvariable [ itcl::scope global_list(track_steplen) ] 
    pack $subframe2.track_steplen -side left -padx 2 -pady 5  
    
    set eigenvector [ iwidgets::optionmenu $subframe2.eigenvector \
			  -command [ itcl::code $this track_eigenvector_cb ] \
			  -labeltext "Eigenvector:" ]	
    pack $eigenvector -side left -padx 2 -pady 5
    $eigenvector insert end "Primary"
    $eigenvector insert end "Secondary"
    $eigenvector insert end "Tertiary"
    set widget_list(track_eigenvector) $eigenvector

    set subframe3 [ frame $frame1.subframe3 ]
    pack $subframe3 -fill both -expand f
    
    label $subframe3.dummy -text "Transformation:"
    pack $subframe3.dummy -side left -expand f

    checkbutton $subframe3.flipx -text "Flip X" -variable [ itcl::scope global_list(track_flipx) ]
    pack $subframe3.flipx -pady 5 -side left
    checkbutton $subframe3.flipy -text "Flip Y" -variable [ itcl::scope global_list(track_flipy) ]
    pack $subframe3.flipy -pady 5 -side left
    checkbutton $subframe3.flipz -text "Flip Z" -variable [ itcl::scope global_list(track_flipz) ]
    pack $subframe3.flipz -pady 5 -side left

    iwidgets::labeledframe $base.frame2 \
	-labelpos nw  \
	-labeltext "Fiber filtering"
    pack $base.frame2 -fill both -expand f
    
    set frame2 [ $base.frame2 childsite ]

    set subframe4 [ frame $frame2.subframe4 ]
    pack $subframe4 -fill both -expand f
    
    iwidgets::entryfield $subframe4.track_minFA \
	-width 4 \
	-validate real \
	-labeltext "Min FA:" \
	-textvariable [ itcl::scope global_list(track_minFA) ] 
    pack $subframe4.track_minFA -side left -padx 5 -pady 5  

    iwidgets::entryfield $subframe4.track_maxFA \
	-width 4 \
	-validate real \
	-labeltext "Max FA:" \
	-textvariable [ itcl::scope global_list(track_maxFA) ] 
    pack $subframe4.track_maxFA -side left -padx 5 -pady 5  

    iwidgets::entryfield $subframe4.track_minMD \
	-width 5 \
	-validate real \
	-labeltext "Min MD:" \
	-textvariable [ itcl::scope global_list(track_minMD) ] 
    pack $subframe4.track_minMD -side left -padx 5 -pady 5  
    
    iwidgets::entryfield $subframe4.track_maxMD \
	-width 5 \
	-validate real \
	-labeltext "Max MD:" \
	-textvariable [ itcl::scope global_list(track_maxMD) ] 
    pack $subframe4.track_maxMD -side left -padx 5 -pady 5  
    
    set subframe5 [ frame $frame2.subframe5 ]
    pack $subframe5 -fill both -expand f

    iwidgets::entryfield $subframe5.track_mindist \
	-width 5 \
	-validate real \
	-labeltext "Min length (mm):" \
	-textvariable [ itcl::scope global_list(track_mindist) ] 
    pack $subframe5.track_mindist -side left -padx 5 -pady 5        

    iwidgets::entryfield $subframe5.track_maxdist \
	-width 5 \
	-validate real \
	-labeltext "Max length (mm):" \
	-textvariable [ itcl::scope global_list(track_maxdist) ] 
    pack $subframe5.track_maxdist -side left -padx 5 -pady 5    

    iwidgets::entryfield $subframe5.track_maxangle \
	-width 5 \
	-validate real \
	-labeltext "Max angle (deg) :" \
	-textvariable [ itcl::scope global_list(track_maxangle) ] 
    pack $subframe5.track_maxangle -side left -padx 5 -pady 5  

    set subframe7 [ frame $frame2.subframe7 ]
    pack $subframe7 -fill both -expand f

    checkbutton $subframe7.track_regionfilter \
	-text "Only keep fibers that end in region" \
	-variable [ itcl::scope global_list(track_regionfilter) ] \
	-command [ itcl::code $this track_regionfilter_cb ]    
    pack $subframe7.track_regionfilter -side left -padx 5

    set track_regionnumber [ iwidgets::spinint $subframe7.track_regionnumber \
				 -fixed 4 -width 4 -range {0 1000} -step 1 \
				 -textvariable \
				 [ itcl::scope global_list(track_regionnumber) ] ]
    pack $track_regionnumber -side left -pady 5 -padx 0
    set widget_list(track_regionnumber) $track_regionnumber  
    $widget_list(track_regionnumber) configure -state disabled
    
    ## clipping
    set track_clipping [ checkbutton $subframe7.track_clipping \
			     -text "Clip fibers" -state disabled\
			     -variable [ itcl::scope global_list(track_clipping) ] ]
    pack $track_clipping -side left -padx 10       
    set widget_list(track_clipping) $track_clipping  

    set trackbtn [ iwidgets::buttonbox $base.trackbtn -orient horizontal ]
    $trackbtn add track -text "Track!" -command [ itcl::code $this fiber_track ] -state disabled
    pack $trackbtn -side left -expand f
    set widget_list(trackbtn) $trackbtn

    ## auto stat
    set track_autostat [ checkbutton $base.track_autostat \
			     -text "Auto fiber statistics" \
			     -variable [ itcl::scope global_list(track_autostat) ] ]
    pack $track_autostat -side right -padx 5  
}

#-------------------------------------------------------------------------------------------
#  Create fiber tracking control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateFiberControl { base } {
    
    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Fibers"
    pack $base.frame0 -fill both
    
    set frame0 [ $base.frame0 childsite ]
    
    set subframe1 [ frame $frame0.subframe1 ]
    pack $subframe1 -fill both -expand t -side top

    label $subframe1.dummy0 -text "Color:"
    pack $subframe1.dummy0 -side left -padx 5 -pady 5
        
    set fiber_color [ image create photo -width 15 -height 15 ]
    $fiber_color put $global_list(fiber_color) -to 0 0 15 15
    
    button $subframe1.fiber_color -image $fiber_color \
	-command [ itcl::code $this fiber_color_cb ]
    pack $subframe1.fiber_color -side left
    set widget_list(fiber_color) $subframe1.fiber_color

    set fiber_name [ iwidgets::entryfield $subframe1.fiber_name \
			 -textvariable [ itcl::scope global_list(fiber_name) ] \
			 -labeltext "Label:" \
			 -width 30 \
			 -command [ itcl::code $this fiber_name_cb ] ] 
    pack $fiber_name -side left -pady 5 -padx 5 -fill x -expand t
    set widget_list(fiber_name) $fiber_name
    
    checkbutton $subframe1.fiber_display \
	-text "Display" \
	-variable [ itcl::scope global_list(fiber_display) ] \
	-command [ itcl::code $this fiber_display_cb ]    
    pack $subframe1.fiber_display -side left -padx 5

    set middle [ frame $frame0.middle ] 
    pack $middle -fill x -expand t -side top -pady 2
    
    set subframe2 [ frame $middle.subframe2 ]
    pack $subframe2 -fill both -expand t -side left -pady 2

    iwidgets::scrolledlistbox $subframe2.fiberlist \
	-hscrollmode dynamic \
	-visibleitems 40x15 \
	-listvariable [ itcl::scope global_list(fiber_list) ] \
	-selectmode extended \
	-selectioncommand [ itcl::code $this fiber_selection_cb ]    
    pack $subframe2.fiberlist -side left -fill both -expand t -padx 5
    set widget_list(fiber_list) $subframe2.fiberlist
   
    set subframe3 [ frame $middle.subframe3 ]
    pack $subframe3 -expand f -side left

    set fiberbbox [ iwidgets::buttonbox $subframe3.fiberbbox -orient vertical ]
    $fiberbbox add load -text "Load" -command [ itcl::code $this fiber_load ]
    $fiberbbox add save -text "Save" -command [ itcl::code $this fiber_saveselected ]
    $fiberbbox add saveall -text "Save all" -command [ itcl::code $this fiber_saveall ]
    $fiberbbox add remove -text "Remove" -command [ itcl::code $this fiber_removeselected ]
    $fiberbbox add clear -text "Clear" -command [ itcl::code $this fiber_clear ]
    $fiberbbox add stat -text "Statistics" -command [ itcl::code $this fiber_statistics ]
    pack $fiberbbox -side left -expand t
    set widget_list(fiber_buttonbox) $fiberbbox        
}

#-------------------------------------------------------------------------------------------
#  Create statistics control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateStatisticsControl { base } {

    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Statistics"
    pack $base.frame0 -fill both -expand t 
    
    set frame0 [ $base.frame0 childsite ]

    set subframe0 [ frame $frame0.subframe0 ]
    pack $subframe0 -fill both -expand t

    set stat_text [iwidgets::scrolledtext $subframe0.stat_text \
		       -visibleitems 70x19 \
		       -foreground black \
		       -vscrollmode static \
		       -hscrollmode static \
		       -wrap none ]
    pack $stat_text -side top -expand t -fill both -padx 2 -pady 2
    set widget_list(stat_text) $stat_text

    set subframe1 [ frame $frame0.subframe1 ]
    pack $subframe1 -fill both -expand t

    set statbbox [ iwidgets::buttonbox $subframe1.statbbox -orient horizontal -pady 2 -padx 2 ]
    $statbbox add save -text "Save..." -command [ itcl::code $this fiber_savestatistics ]
    $statbbox add clear -text "Clear" -command "$stat_text delete 1.0 end"
    pack $statbbox -side left -fill both 

    set stat_overwrite [ checkbutton $subframe1.stat_overwrite -text "Overwrite" \
			     -variable [ itcl::scope global_list(stat_overwrite) ] ]
    pack $stat_overwrite -side right -padx 5       
}


#-------------------------------------------------------------------------------------------
#  Create fiber display control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateFiberDisplayControl { base } {

    set subframe0 [ frame $base.subframe0 ]
    pack $subframe0 -fill x -side top -expand f -padx 5 -pady 5
    
    set view_fiber_filter \
	[ iwidgets::optionmenu $subframe0.view_fiber_filter -labeltext "Filter:" ]
    
    $view_fiber_filter configure -command [ itcl::code $this view_fiber_filter_cb ]
    $view_fiber_filter insert end "Fractional anisotropy"
    $view_fiber_filter insert end "Mean diffusivity"
    $view_fiber_filter insert end "Fiber distance"
    $view_fiber_filter insert end "Fiber angle"
    $view_fiber_filter insert end "Region number"
    $view_fiber_filter insert end "Input map"
    $view_fiber_filter insert end "Directionality"
    
    pack $view_fiber_filter -side left -padx 5
    set widget_list(view_fiber_filter) $view_fiber_filter
    
    set view_fiber_glyph [ iwidgets::optionmenu $subframe0.view_fiber_glyph \
				-labeltext "View fibers as:"   \
				-command [ itcl::code $this view_fiber_glyph_cb ] ] \
	
    pack $view_fiber_glyph -side left -padx 5
    $view_fiber_glyph insert end "Lines"
    $view_fiber_glyph insert end "Tubes"
    $view_fiber_glyph insert end "Points"
    set widget_list(view_fiber_glyph) $view_fiber_glyph
    
    set subframe1 [ frame $base.subframe1 ]
    pack $subframe1 -side top -fill both -padx 5 -expand f
    
    iwidgets::entryfield $subframe1.view_fiber_tuberadius \
	-width 5 \
	-validate real \
	-labeltext "Tube radius:" \
	-textvariable [ itcl::scope global_list(view_fiber_tuberadius) ]  \
	-command [ itcl::code $this view_fiber_tuberadius_cb ]
    pack $subframe1.view_fiber_tuberadius -side left -padx 5    

    iwidgets::entryfield $subframe1.view_fiber_pointsize \
	-width 5 \
	-validate real \
	-labeltext "Point size:" \
	-textvariable [ itcl::scope global_list(view_fiber_pointsize) ]  \
	-command [ itcl::code $this view_fiber_pointsize_cb ]
    pack $subframe1.view_fiber_pointsize -side left -padx 5    
    
    set view_fiber_luk [ checkbutton $subframe1.view_fiber_luk \
			     -text "Apply colormap" \
			     -variable [ itcl::scope global_list(view_fiber_luk) ] \
			     -command [ itcl::code $this view_fiber_colormap_cb ] ]
    pack $view_fiber_luk -side left -pady 5 -padx 5       
    set widget_list(view_fiber_luk) $view_fiber_luk

    set subframe2 [ frame $base.subframe2 ]
    pack $subframe2 -side left -fill both -padx 5 
    
    set subframe21 [ frame $subframe2.subframe21 ]
    pack $subframe21 -side top -fill both -expand t -padx 5 

    set fiber_luk [ mpjguilookuptable \#auto 0 ]
    $fiber_luk Initialize $subframe21
    pack forget [ $fiber_luk GetButtonBoxWidget ]
#    pack forget [ $fiber_luk GetMenuBar ]     
    #    [ $fiber_luk GetLookupTable ] SetVectorModeToComponent
    #    [ $fiber_luk GetLookupTable ] SetVectorComponent 0
    $fiber_luk SetHueRange 0.5 0.92
    $fiber_luk SetSaturationRange 0.75 1.0
    $fiber_luk Update
    $fiber_luk Show
    

    set widget_list(view_fiberluk) $fiber_luk
}

#-------------------------------------------------------------------------------------------
#  Create display control
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateDisplayControl { base } {

    iwidgets::labeledframe $base.frame0 \
	-labelpos nw  \
	-labeltext "Properties"
    pack $base.frame0 -fill both -expand t 

    set frame0 [ $base.frame0 childsite ]
    
    set subframe0 [ frame $frame0.subframe0 ]
    pack $subframe0 -fill both -padx 5 -pady 5 -expand f
    
    checkbutton $subframe0.view_fibers -text "View fibers" \
	-variable [ itcl::scope global_list(view_fiber) ] \
	-command [ itcl::code $this view_toggle_fiber ]   
    pack $subframe0.view_fibers -side left -padx 5

    checkbutton $subframe0.view_colorbar -text "View colorbar" \
	-variable [ itcl::scope global_list(view_colorbar) ] \
	-command [ itcl::code $this view_toggle_colorbar ]   
    pack $subframe0.view_colorbar -side left -padx 5
    
    iwidgets::entryfield $subframe0.view_colorbar_label \
	-width 15 \
	-labeltext "Input Map label:" \
	-textvariable [ itcl::scope global_list(view_colorbar_label) ]
    pack $subframe0.view_colorbar_label -side left -padx 5    

    set subframe1 [ frame $frame0.subframe1 ] 
    pack $subframe1 -fill both -expand t -padx 2 -pady 2

    CreateFiberDisplayControl $subframe1
}

# -------------------------------------------------------------------------------------------
#  Diffusion control inititialization
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::Initialize { widget } {
    
    if { $initialized == 1 } { return $basewidget }
    
    #  -------------------------------------
    #  Create User Interface
    #  -------------------------------------	
    set basewidget [toplevel $widget ]
    wm geometry $basewidget 650x480
    wm withdraw $basewidget

    set notebook $basewidget.notebook    
    iwidgets::tabnotebook $notebook -tabpos w         
    set widget_list(notebook) $notebook

    set mb [ frame $basewidget.mb ]
    pack $mb -side top -fill x -expand false
    
    CreateMenu $mb

    CreateInputControl      [ $notebook add -label "Input" ] 
    CreateDirectionControl  [ $notebook add -label "Directionality" ]    
    CreateTrackingControl   [ $notebook add -label "Tracking" ]
    CreateFiberControl      [ $notebook add -label "Fibers" ]
    CreateStatisticsControl [ $notebook add -label "Statistics" ]
    CreateResultsControl    [ $notebook add -label "Results" ]
    CreateDisplayControl    [ $notebook add -label "Display" ]
   
    pack $notebook -side top -fill both -expand t -padx 5
    
    $notebook view "Input"
    
    set pbar [ frame $basewidget.pbar -width 400 -height 20 ]
    pack $pbar -side bottom -expand f -fill x
    CreateProgressBar $pbar
    
    set w [ frame $basewidget.w -relief ridge -width 800]
    pack $w -side bottom -fill x -expand false
    
    eval "button $w.close -text Close -command { $this DismissWindow }"
    pack $w.close -side right -fill x -padx 5 -pady 10
    
    set initialized 1

    SetTitle $appname

    eval "wm protocol $basewidget WM_DELETE_WINDOW { $this DismissWindow }"
 
    return $basewidget
}

#-------------------------------------------------------------------------------------------
#  Create top level menu button
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::CreateMenu { mb } {    
    
    menubutton $mb.results       -text Results     -menu $mb.results.m -underline 0
    pack $mb.results    -side left
    menubutton $mb.tracking -text Tracking -menu $mb.tracking.m -underline 0
    pack $mb.tracking -side left
    menubutton $mb.help      -text Help         -menu $mb.help.m -underline 0 -padx 4
    pack $mb.help  -side right

    menu $mb.results.m -tearoff 0 
#    eval "$mb.results.m add command -label \"Track fibers\" -command { $this fiber_track } -underline 0 -state disabled"
    $mb.results.m add command -label "Save All Results" -command [ itcl::code $this saveallresults ] -underline 0
    $mb.results.m add separator
    if { $parent == 0 } {
	eval "$mb.results.m add command -label Exit -command {  pxtkexit } -underline 1"
    } else {
	eval "$mb.results.m add command -label Close -command {  $this HideWindow } -underline 1"
    }

    menu $mb.tracking.m -tearoff 0
    $mb.tracking.m add command -label "Track fibers" -command [ itcl::code $this fiber_track ] -underline 0 -state disabled
    $mb.tracking.m add separator
    $mb.tracking.m add check -label "Exploration" -underline 0 \
	-variable [ itcl::scope global_list(track_explore) ] \
	-command [ itcl::code $this view_toggle_trackexplore ]

    menu $mb.help.m -tearoff 0

    set widget_list(menu_results) $mb.results.m    
    set widget_list(menu_tracking) $mb.tracking.m

    eval "$mb.help.m add command -label About -command { $this AboutCommand }"
   
}

#-------------------------------------------------------------------------------------------
#  Add controls to menu button
# ------------------------------------------------------------------------------------------
itcl::body mpjfibertracking::AddToMenuButton { mb args } {
    eval "$mb add command -label \"Fiber tracking\" -command {$this ShowWindow \"Input\"}"
}

#-------------------------------------------------------------------------------------------
#  Main function for stand-alone execution
# ------------------------------------------------------------------------------------------
if { [ file rootname $argv0 ] == [ file rootname [ info script ] ] } {
    puts "\n[ file rootname $argv0 ] is not a stand-alone program.\n"
    exit

}
